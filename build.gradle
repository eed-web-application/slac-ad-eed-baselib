plugins {
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'edu.stanford.slac'
version = '0.0.1'
def javaSourceCompatibility = JavaVersion.VERSION_21
def javaTargetCompatibility = JavaVersion.VERSION_21

java {
    sourceCompatibility = javaSourceCompatibility
    targetCompatibility = javaTargetCompatibility

}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.boot:spring-boot-starter-data-ldap',
            'org.springframework.boot:spring-boot-starter-security',
            'org.springframework.data:spring-data-commons',
            'io.micrometer:micrometer-registry-prometheus',
            'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0',
            'org.springdoc:springdoc-openapi-starter-webflux-ui:2.3.0',
            'javax.annotation:javax.annotation-api:1.3.2',
            'io.swagger.core.v3:swagger-annotations:2.2.18',
            'org.mapstruct:mapstruct:1.5.5.Final',
            'com.fasterxml.jackson.core:jackson-databind',
            'io.jsonwebtoken:jjwt-root:0.12.3',
            'io.jsonwebtoken:jjwt-api:0.12.3',
            'io.jsonwebtoken:jjwt-impl:0.12.4',
            'io.jsonwebtoken:jjwt-jackson:0.12.3',
            'io.jsonwebtoken:jjwt-extensions:0.12.4',
            'com.unboundid:unboundid-ldapsdk:6.0.11'

    implementation 'org.silentsoft:badge4j:1.11.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.awaitility:awaitility:4.2.0'
}

springBoot {
    buildInfo()
}


tasks.named('test') {
    useJUnitPlatform()
}
jacoco {
    reportsDirectory = layout.buildDirectory.dir('coverage')
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "Github"
            url = "https://maven.pkg.github.com/eed-web-application/slac-ad-eed-baselib"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {
    // Exclude a specific class file from the JAR
    exclude 'edu/stanford/slac/ad/eed/baselib/App.class'
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

testlogger {
    theme 'standard'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showOnlySlow false
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
    logLevel 'lifecycle'
}